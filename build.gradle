plugins { // Latest loom at https://modmuss50.me/fabric.html
    id 'fabric-loom' version '1.14-SNAPSHOT'
    id 'maven-publish'
}

base {
    archivesName = project.archives_base_name + "-" + project.minecraft_version
}

version = project.mod_version + '-' + project.minecraft_version
group = project.maven_group

repositories {
    mavenLocal()
    maven {
        description = "Maven for Cardinal Components and Player Ability Lib"
        url = "https://maven.ladysnake.org/releases/"
    }
    maven {
        description = "Maven for ModMenu and EMI"
        url = "https://maven.terraformersmc.com/releases/"
    }
}

dependencies {
    minecraft ("com.mojang:minecraft:${project.minecraft_version}")
    mappings ("net.fabricmc:yarn:${project.yarn_mappings}:v2")
    modImplementation ("net.fabricmc:fabric-loader:${project.loader_version}")
    modImplementation ("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")

    modImplementation ("com.terraformersmc:modmenu:${project.modmenu_version}")
    modImplementation include ("org.ladysnake.cardinal-components-api:cardinal-components-base:${project.cca_version}")
    modImplementation include ("org.ladysnake.cardinal-components-api:cardinal-components-entity:${project.cca_version}")
}

loom {
    accessWidenerPath = file("src/main/resources/mialib.accesswidener")
}

fabricApi {
    configureDataGeneration() {
        modId = "mialib"
        client = true
    }
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = 21
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    archives_base_name = project.archives_base_name
    withSourcesJar()
}

jar {
    from("LICENSE.md") {
        rename { "${it}_${base.archivesName.get()}"}
    }
}

publishing {
    publications { //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) { //noinspection GroovyAssignabilityCheck
            from components.java
        }
    }
    repositories {
        maven {
            name = "willBlRepositoryReleases"
            url = "https://maven.willbl.dev/releases"
            credentials(PasswordCredentials)
            authentication { //noinspection GroovyAssignabilityCheck
                basic(BasicAuthentication)
            }
        }
    }
}